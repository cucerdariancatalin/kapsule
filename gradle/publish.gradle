apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'org.jetbrains.dokka'

ext {
    isReleaseVersion = !version.endsWith("-SNAPSHOT")
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from dokka
}

dokka {
    outputFormat = 'html'
    outputDirectory = "$buildDir/javadoc"
    includes = ['dokka-module.md', 'dokka-package.md']

    linkMapping {
        dir = "src/main/kotlin"
        url = "https://github.com/gouline/kapsule/blob/master/${project.name}/src/main/kotlin"
        suffix = "#L"
    }
}

artifacts {
    archives sourcesJar, javadocJar
}

def pomConfig = {
    licenses {
        license {
            name project['publish.license.name']
            url project['publish.license.url']
        }
    }
    developers {
        developer {
            id project['publish.developer.id']
            name project['publish.developer.name']
        }
    }
    scm {
        connection "scm:git:${project['publish.vcsHttp']}"
        developerConnection "scm:git:${project['publish.vcsSsh']}"
        url project['publish.vcsUrl']
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            groupId project['publish.groupId']
            artifactId project['publish.artifactId']
            version project['publish.version']
            pom.withXml {
                def root = asNode()
                root.appendNode('name', project['publish.name'])
                root.appendNode('description', project['publish.description'])
                root.appendNode('url', project['publish.url'])
                root.children().last() + pomConfig
            }
        }
    }
}

if (hasProperty('bintray.user') && hasProperty('bintray.key') &&
        hasProperty('ossrh.username') && hasProperty('ossrh.password')) {
    bintray {
        user = project['bintray.user']
        key = project['bintray.key']
        publications = ['mavenJava']

        dryRun = false
        publish = true

        pkg {
            name = project['bintray.package']
            repo = project['bintray.repo']
            userOrg = project['bintray.userOrg']
            desc = project['publish.description']
            licenses = [project['publish.license.name']]
            websiteUrl = project['publish.url']
            vcsUrl = project['publish.vcsHttp']
            version {
                released = new Date()
                name = project['publish.version']
                vcsTag = "v${project['publish.version']}"
                gpg {
                    sign = true
                }
                mavenCentralSync {
                    user = project['ossrh.username']
                    password = project['ossrh.password']
                }
            }
        }
    }
}
