apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: 'org.jetbrains.dokka'

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from dokka
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    required { gradle.taskGraph.hasTask(tasks.uploadArchives) }
    sign configurations.archives
}

dokka {
    outputFormat = 'html'
    outputDirectory = "$buildDir/javadoc"
    includes = ['dokka-module.md', 'dokka-package.md']

    linkMapping {
        dir = "src/main/kotlin"
        url = "https://github.com/traversals/kapsule/blob/master/${project.name}/src/main/kotlin"
        suffix = "#L"
    }
}

if (hasProperty('ossrh.username') && hasProperty('ossrh.password')) {
    uploadArchives {
        repositories {
            mavenDeployer {
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                pom.groupId = project['publish.groupId']
                pom.artifactId = project['publish.artifactId']
                pom.version = project['publish.version']

                repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                    authentication(userName: project['ossrh.username'], password: project['ossrh.password'])
                }

                snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                    authentication(userName: project['ossrh.username'], password: project['ossrh.password'])
                }

                pom.project {
                    name project['pom.name']
                    description project['pom.description']
                    packaging 'jar'
                    url project['pom.url']

                    licenses {
                        license {
                            name project['pom.licence.name']
                            url project['pom.licence.url']
                        }
                    }

                    developers {
                        developer {
                            id project['pom.developer.id']
                            name project['pom.developer.name']
                        }
                    }

                    scm {
                        url project['pom.scm.url']
                        connection project['pom.scm.connection']
                        developerConnection project['pom.scm.developerConnection']
                        tag 'HEAD'
                    }
                }
            }
        }
    }
}
